plugins {
	id "groovy"
	id "eclipse"
	id "application"
	id "jacoco"
}

mainClassName = 'net.carlosgsouza.dependencygraph.DependencyGraphService'

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
	
	performanceTestCompile.extendsFrom testCompile
	performanceTestRuntime.extendsFrom testRuntime
}

repositories {
	jcenter()
}

dependencies {
	compile "org.codehaus.groovy:groovy:2.3.10"

	testCompile "org.spockframework:spock-core:1.0-groovy-2.3"
	testCompile "cglib:cglib-nodep:2.2.2"
	testCompile "org.objenesis:objenesis:1.2"
}

sourceSets {
	integrationTest {
		groovy {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file("src/integrationTest/groovy")
		}
	}
	
	performanceTest {
		groovy {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/performanceTest/groovy')
		}
	}
}

task integrationTest(type:Test, dependsOn:"installApp") {
	testClassesDir = sourceSets.integrationTest.output.classesDir
	classpath = sourceSets.integrationTest.runtimeClasspath
	outputs.upToDateWhen{ false }
}
check.dependsOn integrationTest

task performanceTest(type:Test) {
	testClassesDir = sourceSets.performanceTest.output.classesDir
	classpath = sourceSets.performanceTest.runtimeClasspath
	outputs.upToDateWhen{ false }
}
check.dependsOn performanceTest

tasks.withType(Test) {
	reports.html.destination = file("${reporting.baseDir}/${name}")
}

test {
	testLogging {
		exceptionFormat "full"
	}
}

task wrapper(type:Wrapper) {
	gradleVersion="2.3"
}